# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=mpv-build-git
pkgver=0.39.0.315.g91d4e6f937
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (uses statically linked ffmpeg). (GIT version)"
arch=('x86_64')
depends=(
  'gcc-libs'
  'glibc'
  'bash'
  'fribidi' 'libfribidi.so'
  'fontconfig' 'libfontconfig.so'
  'freetype2' 'libfreetype.so'
  'harfbuzz' 'libharfbuzz.so'
  'libunibreak' # libunibreak.so
  'gnutls'
  'jack' 'libjack.so'
  'lcms2' 'liblcms2.so'
  'libarchive' 'libarchive.so'
  'libbluray' 'libbluray.so'
  'libdvdread' #libdvdread.so
  'libdvdnav' #'libdvdnav.so'
  'libbs2b' 'libbs2b.so'
  'libcaca'
  'libcdio'
  'libcdio-paranoia'
  'dav1d' 'libdav1d.so'
  'libdovi' 'libdovi.so'
  'libgme'
  'libmysofa'
  'libpulse' 'libpulse.so'
  'shaderc' 'libshaderc_shared.so'
  'libsixel'
  'libsoxr'
  'libssh' 'libssh.so'
  'libva' 'libva.so' 'libva-drm.so' 'libva-wayland.so' 'libva-x11.so'
  'libvdpau'
  'libxkbcommon' 'libxkbcommon.so'
  'libx11'
  'libxcb'
  'libxext'
  'libxpresent'
  'libxrandr'
  'libxss'
  'libxv'
  'zimg' 'libzimg.so'
  'luajit'
  'mujs'
  'openal' 'libopenal.so'
  'rubberband' 'librubberband.so'
  'libpipewire' 'libpipewire-0.3.so'
  'sdl2'
  'sndio' 'libsndio.so'
  'uchardet'
  'v4l-utils'
  'davs2'
  'ffnvcodec-headers'
  'wayland-protocols'
  'alsa-lib' 'libasound.so'
  'libglvnd' 'libEGL.so' 'libGL.so'
  'glslang' #'libSPIRV.so'
  'libdrm'
  'bzip2' 'libbz2.so'
  'libunwind'
  'xz' 'liblzma.so'
  'libmysofa' 'libmysofa.so'
  'wayland' 'libwayland-client.so' 'libwayland-cursor.so' 'libwayland-egl.so'
  'libjpeg-turbo' 'libjpeg.so'
  'vulkan-icd-loader' 'libvulkan.so'
  'libdisplay-info' 'libdisplay-info.so'
  'mesa'
  'zlib' 'libz.so'
  'python'
  'hicolor-icon-theme'
)

license=('GPL2' 'GPL3' 'LGPL3' 'LGPL2.1' 'BSD')
url='http://mpv.io'
makedepends=(
  'git'
  'meson'
  'python-docutils'
  'nasm'
  'ladspa'
  'vulkan-headers'
  'ffnvcodec-headers-git'
  'python-mako'
  'python-jinja'
  'python-markupsafe'
  'nuklear'
  'glad'
  'jbigkit'
  'xxhash'
)
optdepends=(
  'nvidia-utils: for hardware accelerated video decoding with CUDA'
  'youtube-dl: Another way to view youtube videos with mpv'
  'yt-dlp: Another way to view youtuve videos with mpv'
  'zsh-completions: Additional completion definitions for Zsh users'
  'bash-completion: Additional completion definitions for Bash users'
)
provides=(
  'mpv'
  'libmpv.so'
)
conflicts=('mpv')
options=('!emptydirs' '!lto' 'debug')
source=(
  'git+https://github.com/mpv-player/mpv-build.git'
  'git+https://github.com/mpv-player/mpv.git'
  'git+https://github.com/ffmpeg/ffmpeg.git'
  'git+https://github.com/libass/libass.git'
  'git+https://github.com/haasn/libplacebo.git'
  'git+https://github.com/fastfloat/fast_float.git'
)
sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
)
backup=('etc/mpv/encoding-profiles.conf')

if [ -f /usr/lib/libvapoursynth.so ]; then
  depends+=('vapoursynth')
fi
if [ -f /usr/lib/libavisynth.so.*.*.* ]; then
  depends+=('avisynthplus')
fi

pkgver() {
  cd mpv
  echo "$(git describe --long --tags | tr - . | tr -d v)"
}

prepare() {
  cd mpv-build
  # use arch-meson
  sed -e 's|meson setup|arch-meson|g' \
      -e 's|-Dbuildtype=release|--auto-features disabled|g' \
      -e '/LDFLAGS=/s/^/#/' \
      -i scripts/{mpv,libplacebo}-config

  git clone "${srcdir}/mpv"
  git clone "${srcdir}/ffmpeg"
  git clone "${srcdir}/libass"
  git clone "${srcdir}/libplacebo"

  # Set libplacebo/ffmpeg/libass/mpv flags

  _libplacebo_options=(
    '-Dvulkan=enabled'
    '-Dglslang=enabled'
    '-Dshaderc=enabled'
    '-Dlcms=enabled'
    '-Dd3d11=disabled'
    )

  _ffmpeg_options=(
    '--disable-libopencv'
    '--disable-libtls'
    '--disable-mbedtls'
    '--disable-programs'
    '--disable-amf'
    '--disable-liblensfun'
    '--disable-libtensorflow'
    
    # Essentials and libraries
    '--enable-alsa'
    '--enable-bzlib'
    '--enable-chromaprint'
    '--enable-fontconfig'
    '--enable-frei0r'
    '--enable-gcrypt'
    '--enable-gmp'
    '--enable-gnutls'
    '--enable-gpl'
    '--enable-gray'
    '--enable-iconv'
    '--enable-ladspa'
    '--enable-lcms2'
    '--enable-libaom'
    '--enable-libaribb24'
    '--enable-libass'
    '--enable-libbluray'
    '--enable-libbs2b'
    '--enable-libcaca'
    '--enable-libcdio'
    '--enable-libcodec2'
    '--enable-libdav1d'
    '--enable-libdc1394'
    '--enable-libdrm'
    '--enable-libfdk-aac'
    '--enable-libfreetype'
    '--enable-libfribidi'
    '--enable-libglslang'
    '--enable-libgme'
    '--enable-libgsm'
    '--enable-libiec61883'
    '--enable-libilbc'
    '--enable-libjack'
    '--enable-libjxl'
    '--enable-libkvazaar'
    '--enable-libmodplug'
    '--enable-libmp3lame'
    '--enable-libmysofa'
    '--enable-libopencore-amrnb'
    '--enable-libopencore-amrwb'
    '--enable-libopenh264'
    '--enable-libopenjpeg'
    '--enable-libopenmpt'
    '--enable-libopus'
    '--enable-libpulse'
    '--enable-librabbitmq'
    '--enable-librav1e'
    '--enable-librsvg'
    '--enable-librtmp'
    '--enable-librubberband'
    '--enable-libsnappy'
    '--enable-libsoxr'
    '--enable-libspeex'
    '--enable-libsrt'
    '--enable-libssh'
    '--enable-libsvtav1'
    '--enable-libtesseract'
    '--enable-libtheora'
    '--enable-libtwolame'
    '--enable-libv4l2'
    '--enable-libvidstab'
    '--enable-libvmaf'
    '--enable-libvorbis'
    '--enable-libvpx'
    '--enable-libwebp'
    '--enable-libx264'
    '--enable-libx265'
    '--enable-libxcb'
    '--enable-libxcb-shape'
    '--enable-libxcb-shm'
    '--enable-libxcb-xfixes'
    '--enable-libxml2'
    '--enable-libxvid'
    '--enable-libzimg'
    '--enable-libzmq'
    '--enable-libzvbi'
    '--enable-libvpl'
    
    # Optimization and compatibility
    '--enable-lto'
    '--enable-lv2'
    '--enable-lzma'
    '--enable-nonfree'
    '--enable-omx'
    '--enable-openal'
    '--enable-opencl'
    '--enable-opengl'
    '--enable-pic'
    '--enable-sdl2'
    '--enable-sndio'
    '--enable-v4l2-m2m'
    '--enable-vaapi'
    '--enable-vapoursynth'
    '--enable-vdpau'
    '--enable-version3'
    '--enable-vulkan'
    '--enable-xlib'
    '--enable-zlib'
    )

  _mpv_options=(
    '-Db_lto=true'
    '-Db_staticpic=true'
    '-Ddefault_library=shared'
    '-Dcplayer=true'
    '-Dlibmpv=true'
    '-Dbuild-date=true'
    
    # Core dependencies
    '-Dcdda=enabled'
    '-Dcplugins=enabled'
    '-Ddvbin=enabled'
    '-Ddvdnav=enabled'
    '-Diconv=enabled'
    '-Djavascript=enabled'
    '-Dlcms2=enabled'
    '-Dlibarchive=enabled'
    '-Dlibavdevice=enabled'
    '-Dlibbluray=enabled'
    '-Dlua=luajit'
    '-Drubberband=enabled'
    '-Dsdl2=enabled'
    '-Dsdl2-gamepad=enabled'
    '-Duchardet=enabled'
    '-Duwp=disabled'
    '-Dvapoursynth=enabled'
    '-Dvector=auto'
    '-Dwin32-threads=disabled'
    '-Dzimg=enabled'
    '-Dzlib=enabled'
    
    # Audio backends
    '-Dalsa=enabled'
    '-Daudiounit=disabled'
    '-Davfoundation=disabled'
    '-Dcoreaudio=disabled'
    '-Djack=enabled'
    '-Dopenal=enabled'
    '-Dopensles=disabled'
    '-Doss-audio=disabled'
    '-Dpipewire=enabled'
    '-Dpulse=enabled'
    '-Dsdl2-audio=enabled'
    '-Dsndio=enabled'
    '-Dwasapi=disabled'
    
    # Video backends and GPU acceleration
    '-Dcaca=enabled'
    '-Dcocoa=disabled'
    '-Dd3d11=disabled'
    '-Ddirect3d=disabled'
    '-Ddmabuf-wayland=enabled'
    '-Ddrm=enabled'
    '-Degl=enabled'
    '-Degl-android=disabled'
    '-Degl-angle=disabled'
    '-Degl-angle-lib=disabled'
    '-Degl-angle-win32=disabled'
    '-Degl-drm=enabled'
    '-Degl-wayland=enabled'
    '-Degl-x11=enabled'
    '-Dgbm=enabled'
    '-Dgl=enabled'
    '-Dgl-cocoa=disabled'
    '-Dgl-dxinterop=disabled'
    '-Dgl-win32=disabled'
    '-Dgl-x11=enabled'
    '-Djpeg=enabled'
    '-Dsdl2-video=enabled'
    '-Dshaderc=disabled'
    '-Dsixel=enabled'
    '-Dspirv-cross=disabled'
    '-Dplain-gl=enabled'
    '-Dvdpau=enabled'
    '-Dvdpau-gl-x11=enabled'
    '-Dvaapi=enabled'
    '-Dvaapi-drm=enabled'
    '-Dvaapi-wayland=enabled'
    '-Dvaapi-x11=enabled'
    '-Dvaapi-win32=disabled'
    '-Dvulkan=enabled'
    '-Dwayland=enabled'
    '-Dx11=enabled'
    '-Dxv=enabled'
    
    # Explicitly disabled flags irrelevant to Arch/Wayland
    '-Dandroid-media-ndk=disabled'
    '-Dd3d-hwaccel=disabled'
    '-Dd3d9-hwaccel=disabled'
    '-Dgl-dxinterop-d3d9=disabled'
    '-Dios-gl=disabled'
    '-Dvideotoolbox-gl=disabled'
    '-Dvideotoolbox-pl=disabled'
    '-Dmacos-10-15-4-features=disabled'
    '-Dmacos-11-features=disabled'
    '-Dmacos-11-3-features=disabled'
    '-Dmacos-12-features=disabled'
    '-Dmacos-cocoa-cb=disabled'
    '-Dmacos-media-player=disabled'
    '-Dmacos-touchbar=disabled'
    '-Dswift-build=disabled'
    '-Dwin32-smtc=disabled'
    
    # Documentation (enabled for reference)
    '-Dmanpage-build=disabled'
    '-Dhtml-build=enabled'
    '-Dpdf-build=disabled'
    
    # Fix Build
    '-Dc_link_args=-lstdc++ -lglslang -Wl,-Bsymbolic'
    )

if [ -f /usr/lib/libavisynth.so.*.*.* ]; then
   _ffmpeg_options+=('--enable-avisynth')
fi
if [ -f /usr/lib/libvapoursynth.so ]; then
  _ffmpeg_options+=('--enable-vapoursynth')
  _mpv_options+=('-Dvapoursynth=enabled')
fi

  (IFS=$'\n'; echo "${_libplacebo_options[*]}" > libplacebo_options )
  (IFS=$'\n'; echo "${_ffmpeg_options[*]}" > ffmpeg_options )
  (IFS=$'\n'; echo "${_mpv_options[*]}" > mpv_options )

  cd libplacebo
  git config submodule.3rdparty/fast_float.url "${srcdir}/fast_float"
  git -c protocol.file.allow=always submodule update --init \
     3rdparty/fast_float
}

build() {
  cd mpv-build
  ./build
}

package() {
  cd mpv-build
  DESTDIR="${pkgdir}" ./install

  install -Dm755 mpv/TOOLS/mpv_identify.sh "${pkgdir}/usr/bin/mpv-identify"
  install -Dm755 mpv/TOOLS/idet.sh "${pkgdir}/usr/bin/mpv-idet"
  install -Dm755 mpv/TOOLS/umpv "${pkgdir}/usr/bin/umpv"

  install -Dm644 mpv/DOCS/encoding.rst "${pkgdir}/usr/share/doc/mpv/encoding.rst"
  install -Dm644 mpv/DOCS/edl-mpv.rst "${pkgdir}/usr/share/doc/mpv/edl-mpv.rst"
  install -Dm644 mpv/DOCS/client-api-changes.rst "${pkgdir}/usr/share/doc/mpv/client-api-changes.rst"
  install -Dm644 mpv/DOCS/contribute.md "${pkgdir}/usr/share/doc/mpv/contribute.md"

  if [ -f "${pkgdir}/usr/share/doc/mpv/mpv.conf" ]; then
      sed 's|/usr/local/etc/mpv.conf|/etc/mpv.conf|g' -i "${pkgdir}/usr/share/doc/mpv/mpv.conf"
  fi

  (cd mpv/TOOLS/lua; for i in $(find . -type f); do install -Dm644 "${i}" "${pkgdir}/usr/share/mpv/scripts/${i}"; done)

  install -Dm644 mpv/LICENSE.GPL "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.GPL"
  install -Dm644 mpv/LICENSE.LGPL "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.LGPL"
}
